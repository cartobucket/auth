import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'io.quarkus'
    id 'org.openapi.generator' version '7.2.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'io.quarkus:quarkus-container-image-jib'
    implementation 'io.quarkus:quarkus-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")

    // Tooling
    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.12'
    implementation 'jakarta.json.bind:jakarta.json.bind-api:3.0.1'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.0'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:4.0.0'
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'

    implementation project(':auth-data')

    testImplementation 'io.quarkus:quarkus-junit5'
}

group 'com.cartobucket.auth'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

def specFile = "$projectDir/src/main/openapi/api.json"

task generateApiServer(type: GenerateTask) {
    def serverOutputDir = "$buildDir/generated/api/server"

    inputs.file specFile
    outputs.dir serverOutputDir

    generatorName = "jaxrs-spec"
    inputSpec = specFile
    // TODO: Once Jakarta is the default this should be able to be removed. DEC 2022
    templateDir = "$projectDir/src/main/openapi/overrides/jaxrs-spec"
    outputDir = serverOutputDir

    apiPackage = "com.cartobucket.auth.generated"
    invokerPackage = "com.cartobucket.auth.invoker.generated"
    modelPackage = "com.cartobucket.auth.model.generated"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary   : "java8",
            generatePom   : "false",
            interfaceOnly : "true",
            returnResponse : "true",

            /*
            Using JSON-B instead of Jackson for serialization.
            JsonbNillable(false) omits null values from JSON output.
            */
            additionalModelTypeAnnotations: "\n@jakarta.json.bind.annotation.JsonbNillable(false)",

            // Generate separate classes for each endpoint "domain"
            useTags: "true"
    ]
}

compileJava.dependsOn tasks.generateApiServer

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/api/server/src/gen/java", "$projectDir/src/main/java"
        }
        resources {
            srcDir "$projectDir/src/main/openapi/"
        }
    }
}