plugins {
	id 'java'
	id 'org.jetbrains.kotlin.jvm' version '2.2.10'
	id 'org.jetbrains.kotlin.plugin.allopen' version '2.2.10'
	id 'org.jetbrains.kotlin.plugin.noarg' version '2.2.10'
	id 'io.quarkus'
	id 'org.openapi.generator' version '7.2.0'
	id("com.diffplug.spotless") version "6.16.0"
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation 'io.quarkus:quarkus-container-image-jib'
	implementation 'io.quarkus:quarkus-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib'
	implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
}

group 'com.cartobucket'
version '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile) {
//	options.deprecation = true
	options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
}

compileJava {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-parameters'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

sourceSets {
	main {
		java {
			srcDirs "$buildDir/generated/api/server/src/gen/java", "$projectDir/src/main/java"
		}
		resources {
			srcDir "$projectDir/src/main/openapi/"
		}
	}
}

// Kotlin compiler configuration
allOpen {
	annotation("jakarta.ws.rs.Path")
	annotation("jakarta.enterprise.context.ApplicationScoped")
	annotation("jakarta.persistence.Entity")
	annotation("io.quarkus.test.junit.QuarkusTest")
}

noArg {
	annotation("jakarta.persistence.Entity")
	annotation("jakarta.persistence.MappedSuperclass")
	annotation("jakarta.persistence.Embeddable")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = "21"
		javaParameters = true
		freeCompilerArgs += ["-Xjsr305=strict"]
	}
}

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
	ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		// don't need to set target, it is inferred from java

		// apply a specific flavor of google-java-format
		googleJavaFormat('1.16.0').aosp().reflowLongStrings()
		// fix formatting of type annotations
		formatAnnotations()
		// make sure every file has the following copyright header.
		// optionally, Spotless can set copyright years by digging
		// through git history (see "license" section below)
		licenseHeader '/* (C)$YEAR */'
	}
	kotlin {
		ktlint()
		licenseHeader '/* (C)$YEAR */'
	}
}
